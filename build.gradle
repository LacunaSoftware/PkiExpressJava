apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.lacunasoftware.pkiexpress'
archivesBaseName = 'pki-express'
version = '1.18.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
}

task fatJar(type: Jar) {
    manifest {
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'pki-express'
                packaging 'jar'
                artifactId 'pki-express'
                description 'Classes to use Lacuna Software PKI Express'
                url 'https://github.com/LacunaSoftware/PkiExpressJava'

                scm {
                    connection 'scm:git:git@github.com:LacunaSoftware/PkiExpressJava.git'
                    developerConnection 'scm:git:git@github.com:LacunaSoftware/PkiExpressJava.git'
                    url 'https://github.com/LacunaSoftware/PkiExpressJava'
                }

                licenses {
                    license {
                        name 'Unlicense'
                    }
                }

                developers {
                    developer {
                        id 'ismaelm'
                        name 'Ismael Medeiros'
                        email 'IsmaelM@lacunasoftware.com'
                    }
                }
            }
        }
    }
}
